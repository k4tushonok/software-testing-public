plugins {
    id 'java'
    id 'jacoco'
    id 'io.qameta.allure' version '2.11.2'
    id 'com.github.spotbugs' version '5.0.9'
}

group = 'org.usvm'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation("io.javalin:javalin:6.4.0")
    implementation("org.slf4j:slf4j-simple:2.0.16")
    implementation("com.google.code.gson:gson:2.12.1")
    implementation("com.fasterxml.jackson.core:jackson-databind:2.17.2")

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation("io.rest-assured:rest-assured:5.5.1")
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation("org.mockito:mockito-core:5.15.2")

    testImplementation("io.qameta.allure:allure-junit5:2.25.0")
    testImplementation("io.qameta.allure:allure-rest-assured:2.25.0")
}

test {
    useJUnitPlatform()
    systemProperty("allure.results.directory", "build/allure-results")
}

jacoco {
    toolVersion = "0.8.11"
}

tasks.jacocoTestReport {
    dependsOn(tasks.test)
    reports {
        xml.required.set(true)
        html.required.set(true)
    }
}

tasks.jacocoTestCoverageVerification {
    dependsOn(tasks.jacocoTestReport)
    violationRules {
        rule {
            limit {
                minimum = 0.80
            }
        }
    }
}

allure {
    report {
        version.set("2.25.0")
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                enabled.set(true)
            }
        }
    }
}

spotbugs {
    toolVersion = '4.7.0'
    effort = 'max'
    reportLevel = 'high'
}

tasks.withType(com.github.spotbugs.SpotBugsTask).configureEach {
    reports {
        html.enabled = true
        xml.enabled = true
    }
}
